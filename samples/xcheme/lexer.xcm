/**
 * A XCHEME Lang lexer.
 */
alias token T_EXTRA as '_';
alias token T_ALPHA as from 'A' to 'Z' | from 'a' to 'z';
alias token T_DIGIT as from '0' to '9';
alias token T_WORD  as T_EXTRA | T_ALPHA | T_DIGIT;
alias token T_END   as not T_WORD;

export token <auto> T_KEYWORDS as map {
  <103> ANY     as 'any',
  <104> FROM    as 'from',
  <105> TO      as 'to',
  <106> MAP     as 'map',
  <107> THEN    as 'then',
  <108> ELSE    as 'else',
  <109> OR      as 'or',
  <110> AND     as 'and',
  <111> NOT     as 'not',
  <112> OPT     as 'opt',
  <113> REPEAT  as 'repeat',
  <114> PLACE   as 'place',
  <115> APPEND  as 'append',
  <116> PREPEND as 'prepend',
  <117> PIVOT   as 'pivot',
  <118> NEXT    as 'next',
  <119> LEFT    as 'left',
  <120> RIGHT   as 'right',
  <121> SYMBOL  as 'symbol',
  <122> SCOPE   as 'scope',
  <123> ERROR   as 'error',
  <124> HAS     as 'has',
  <125> SET     as 'set',
  <126> UNCASE  as 'uncase',
  <127> PEEK    as 'peek',
  <128> SKIP    as 'skip',
  <129> TOKEN   as 'token',
  <130> NODE    as 'node',
  <131> ALIAS   as 'alias',
  <132> AUTO    as 'auto',
  <133> AS      as 'as',
  <134> IMPORT  as 'import',
  <135> EXPORT  as 'export'
} & T_END;

export token <auto> T_SYMBOLS as map {
  <136> ASTERISK      as '*',
  <137> VERTICAL_BAR  as '|',
  <138> AMPERSAND     as '&',
  <139> PERIOD        as '.',
  <140> COMMA         as ',',
  <141> SEMICOLON     as ';',
  <142> OPEN_BRACES   as '{',
  <143> CLOSE_BRACES  as '}',
  <144> GROUP_OPEN    as '(',
  <145> GROUP_CLOSE   as ')',
  <146> CHEVRON_OPEN  as '<',
  <147> CHEVRON_CLOSE as '>'
};

export token <100> T_IDENTIFIER as (T_ALPHA | T_EXTRA) & opt repeat T_WORD;
export token <101> T_NUMBER     as '0' | from '1' to '9' & opt repeat T_DIGIT;
export token <102> T_STRING     as '\'' & repeat ('\\' then * else not '\'' then *) & '\'';
